# Compiler for a subset of Tamsin, written in Tamsin.

# Copyright (c)2014 Chris Pressey, Cat's Eye Technologies.
# Distributed under a BSD-style license; see LICENSE.

# NOTE, VERY INCOMPLETE: it could use:
#   database -- for indent/outdent (minor)
#   $:format or similar, for emitting

# REQUIRES lib/tamsin_scanner.tamsin
# REQUIRES lib/tamsin_parser.tamsin

main = tamsin_parser:parse → AST & tamsin_scanner:skippable & eof &
       compile(AST).

prelude = $:emit('
/*
 * Generated code!  Edit at your own risk!
 * Must be linked with -ltamsin to build.
 */
#include <tamsin.h>

/* global scanner */

struct scanner * scanner;

/* global state: result of last action */

int ok;
struct term *result;
').

postlude = $:emit('
int main(int argc, char **argv) {
    FILE *input = NULL;
    char *filename = argv[1];
    char *buffer = malloc(8193);
    struct term *bufterm = NULL;

    if (filename == NULL) {
        input = stdin;
    } else {
        input = fopen(filename, "r");
    }

    assert(input != NULL);
    while (!feof(input)) {
        int num_read = fread(buffer, 1, 8192, input);
        if (bufterm == NULL) {
            bufterm = term_new(buffer, num_read);
        } else {
            bufterm = term_concat(bufterm, term_new(buffer, num_read));
        }
    }

    scanner = scanner_new(bufterm->atom, bufterm->size);
    ok = 0;
    result = term_new_from_cstring("nil");

    prod_main_main();

    if (ok) {
        term_fput(result, stdout);
        fwrite("\\n", 1, 1, stdout);
        exit(0);
    } else {
        term_fput(result, stderr);
        fwrite("\\n", 1, 1, stderr);
        exit(1);
    }
}
').

emitln(X) = $:emit(X) & $:emit('\n').
emit(X) = $:emit(X).
indent = 'ok'.
outdent = 'ok'.

compile(program(Ms)) =
    prelude &
    tamsin_parser:find_module('main', Ms) → MainMod &
    get_prods(MainMod) → MMPs &
    compile_all(program(Ms), MMPs) &
    postlude &
    ''.

get_prods(module(N, Ps)) = Ps.

emit_prototypes(nil) = 'ok'.
emit_prototypes(list(production(N, Bs), MMPs)) =
    emit('void prod_main_') &
    emit(N) &
    emitln('();') &
    emit_prototypes(MMPs).

compile_all(P, nil) = 'ok'.
compile_all(P, list(H,T)) = compile_r(P, H) & compile_all(P, T).

compile_r(P, production(N, Bs)) =
    emit('void prod_main_') &
    emit(N) &
    emitln('() {') &
    indent &
    compile_all(P, Bs) &
    outdent &
    emitln('}').

compile_r(P, prodbranch(Fs, Ls, E)) =
    emitln('/* formals and locals go here... */') &
    compile_r(P, E).

compile_r(P, call(prodref('$', 'expect'), list(T, nil))) =
    emit_term(T, 'temp') &
    emitln('tamsin_expect(scanner, temp);').

compile_r(P, call(prodref('$', 'return'), list(T, nil))) =
    emit_term(T, 'temp') &
    emitln('result = temp;') &
    emitln('ok = 1;').

compile_r(P, call(prodref('$', 'print'), list(T, nil))) =
    emit_term(T, 'temp') &
    emitln('result = temp;') &
    emitln('term_fput(result, stdout);') &
    emitln('fprintf(stdout, "\\n");') &
    emitln('ok = 1;').

compile_r(P, call(prodref(M, N), A)) =
    emit('prod_main_') &
    emit(N) &
    emitln('();').

compile_r(P, and(L, R)) =
    compile_r(P, L) &
    emitln('if (ok) {') &
    indent &
    compile_r(P, R) &
    outdent &
    emitln('}').

compile_r(P, or(L, R)) =
    emitln('{') &
    indent &
    emit_decl_state &
    emit_save_state &
    compile_r(P, L) &
    emitln('if (!ok) {') &
    indent &
    emit_restore_state &
    compile_r(P, R) &
    outdent &
    emitln('}') &
    outdent &
    emitln('}').
compile_r(P, send(R, V)) =
    compile_r(P, R) &
    emit(V) &
    emitln(' = result;').
compile_r(P, set(V, T)) =
    emit_term(T, 'temp') &
    emitln('result = temp;') &
    emit(V) &
    emitln(' = result;') &
    emitln('ok = 1;').
compile_r(P, while(R)) =
    emitln('{') &
    indent &
    emit_decl_state &
    emit_term(atom(nil), 'successful_result') &
    emitln('ok = 1;') &
    emitln('while (ok) {') &
    indent &
    emit_save_state &
    compile_r(P, R) &
    emitln('if (ok) {') &
    indent &
    emitln('successful_result = result;') &
    outdent &
    emitln('}') &
    outdent &
    emitln('}') &     # endwhile
    emit_restore_state &
    emitln('result = successful_result;') &
    emitln('ok = 1;') &
    outdent &
    emitln('}').

emit_decl_state = 'ok'.
emit_save_state = 'ok'.
emit_restore_state = 'ok'.

emit_term(variable(VN), N) =
    emit('struct term *') &
    emit(N) &
    emit(' = ') &
    emit(VN) &
    emitln(';').

emit_term(atom(T), N) =
    emit('struct term *') &
    emit(N) &
    emit(' = term_new_from_cstring("') &
    emit(T) &   # TODO -- escaped
    emitln('");').

emit_term(constructor(T,Ts), N) =
    emit('struct term *') &
    emit(N) &
    emit(' = term_new_from_cstring("') &
    emit(T) &   # TODO -- escaped
    emitln('");').

    #  i = 0
    #  for subterm in reversed(term.contents):
    #      subname = name + str(i)
    #      i += 1
    #      self.emit_term(subterm, subname, pattern=pattern);
    #      self.emit("term_add_subterm(%s, %s);" % (name, subname))
