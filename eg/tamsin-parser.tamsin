# Parser for Tamsin AST, written in Tamsin.
# Distributed under a BSD-style license; see LICENSE.

main = grammar using scanner.


# problem: we can't use `word`, `variable` etc in here, because those
# scan using the char scanner.  we have already scanned them using
# tamsin_scanner.  so we use alnum, upper, startswith.


# big problem: aliases!


grammar    = {"@" & pragma & "."} &
             production & "." &
             {production & "."} & eof & 'ok'.
production = word & ["(" & term & {"," & term} & ")"
                    | "[" & expr0 & "]"] & "=" & expr0.
expr0      = expr1 & {("|" | "||") & expr1}.
expr1      = expr2 & {("&" | "&&") & expr2}.
expr2      = expr3 & ["using" & prodref].
expr3      = expr4 & [("→" | "->") & variable].
expr4      = "(" & expr0 & ")"
           | "[" & expr0 & "]"
           | "{" & expr0 & "}"
           | "!" & expr4
           | "set" & variable & "=" & term
           | "return" & term
           | "fail" & term
           | "print" & term
           | terminal
           | variable & ["←" & term]
           | sq_string
           | prodref & ["(" & [term & {"," & term}] & ")"] & ["@" & term].
term       = term0.
term0      = term1 & {"+" & term1}.
term1      = atom & ["(" & [term & {"," & term}] & ")"]
           | variable.
atom       = word | sq_string.
terminal   = dq_string
           | ("«" | "<<") & term & ("»" | ">>").
prodref    = [modref & "."] & word.
modref     = "$".
pragma     = "alias" & word & word & "=" & prodref
           | "unalias" & word.


word = $.alnum.
variable = $.upper.
sq_string = $.startswith('\'').
dq_string = $.startswith('"').



scanner = scan using $.char.
scan = skippable & (symbol | str('\'') | str('"') | sc_word).
symbol = "&" & "&" & '&&'
       | "|" & "|" & '||'
       | "-" & ">" & '->'
       | "<" & "-" & '<-'
       | "<" & "<" & '<<'
       | ">" & ">" & '>>'
       | "=" | "(" | ")" | "[" | "]" | "{" | "}" | "!" | "|" | "&"
       | "," | "." | "@" | "+" | "$" | "→" | "←" | "«" | "»".
str(Q) = «Q» → T & {(escape | !«Q» & any) → S & T ← T + S} & «Q» &
         return T + Q.
escape = "\\" & "n" & '\n'
       | "\\" & "r" & '\r'
       | "\\" & "t" & '\t'
       | "\\" & "\\" & '\\'
       | "\\" & "'" & '\''
       | "\\" & "\"" & '"'.
sc_word = $.alnum → T & { ($.alnum | "_") → S & T ← T + S } & T.
skippable = {whitespace | comment}.
whitespace = " " | "\t" | "\r" | "\n".
comment = "#" & {!"\n" & any} & "\n".
