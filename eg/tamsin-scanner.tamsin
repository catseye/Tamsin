# Scanner for Tamsin tokens, written in Tamsin.
# Distributed under a BSD-style license; see LICENSE.

main = {scan -> T & print T} & ''.

scanner = scan using $:char.
scan = skippable & (symbol | str('\'') | str('"') | sc_word).
symbol = "&" & "&" & '&&'
       | "|" & "|" & '||'
       | "-" & ">" & '->'
       | "<" & "-" & '<-'
       | "<" & "<" & '<<'
       | ">" & ">" & '>>'
       | "=" | "(" | ")" | "[" | "]" | "{" | "}" | "!" | "|" | "&" | ":"
       | "," | "." | "@" | "+" | "$" | "→" | "←" | "«" | "»".
str(Q) = «Q» → T & {(escape | !«Q» & any) → S & T ← T + S} & «Q» &
         return T + Q.
escape = "\\" & "n" & '\n'
       | "\\" & "r" & '\r'
       | "\\" & "t" & '\t'
       | "\\" & "\\" & '\\'
       | "\\" & "'" & '\''
       | "\\" & "\"" & '"'.
sc_word = $:alnum → T & { ($:alnum | "_") → S & T ← T + S } & T.
skippable = {whitespace | comment}.
whitespace = " " | "\t" | "\r" | "\n".
comment = "#" & {!"\n" & any} & "\n".
